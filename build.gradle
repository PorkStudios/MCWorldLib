/*
 * Adapted from The MIT License (MIT)
 *
 * Copyright (c) 2020-2020 DaPorkchop_
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation
 * files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy,
 * modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software
 * is furnished to do so, subject to the following conditions:
 *
 * Any persons and/or organizations using this software must include the above copyright notice and this permission notice,
 * provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
 * BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

apply plugin: "java"
apply plugin: "maven-publish"

ext {
    //general things
    javaVersion = "1.8"
    minecraftVersion = "1.16.2"

    //dependency things
    jacksonVersion = "2.11.2"
    junitVersion = "4.12"
    lombokVersion = "1.16.20"
    gsonVersion = "2.8.5"
    porklibVersion = "0.5.5-SNAPSHOT"
}

group "net.daporkchop"
version "$minecraftVersion-SNAPSHOT"

compileJava {
    sourceCompatibility = targetCompatibility = "$javaVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
}

dependencies {
    compile "net.daporkchop.lib:binary:$porklibVersion"
    compile "net.daporkchop.lib:collections:$porklibVersion"
    compile "net.daporkchop.lib:compression-zlib:$porklibVersion"
    compile "net.daporkchop.lib:concurrent:$porklibVersion"
    compile "net.daporkchop.lib:nbt:$porklibVersion"
    compile "net.daporkchop.lib:minecraft-text:$porklibVersion"
    compile "net.daporkchop.lib:primitive:$porklibVersion"
    compile "net.daporkchop.lib:random:$porklibVersion"
    compile "net.daporkchop.lib:reflection:$porklibVersion"

    compile "com.google.code.gson:gson:$gsonVersion"

    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testCompile "junit:junit:$junitVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    repositories {
        if (project.hasProperty("daporkchopMavenUsername") && project.hasProperty("daporkchopMavenPassword")) {
            maven {
                credentials {
                    username = "$daporkchopMavenUsername"
                    password = "$daporkchopMavenPassword"
                }

                name = "DaPorkchop_"
                url = version.endsWith("SNAPSHOT") ? "https://maven.daporkchop.net/snapshot/" : "https://maven.daporkchop.net/release/"
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = "mcworldlib"
            version = project.version

            artifact jar {
                classifier null
            }

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

publish.dependsOn(publishToMavenLocal)
build.dependsOn(publish)
